plugins {
    id "io.freefair.lombok" version "5.3.0" //is this needed?
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id "java"
    id "maven-publish"
    id "org.jetbrains.kotlin.jvm" version "1.4.30"
    id "org.springframework.boot" version "2.3.4.RELEASE" //any higher breaks spring-cloud compatibility
    id "org.jetbrains.kotlin.plugin.allopen" version "1.4.30"
    id "org.jetbrains.kotlin.plugin.noarg" version "1.4.30"
}

group = "com.hedvig.generic"
version = "0.0.1-SNAPSHOT"
description = "Hedvig's claims management service"
sourceCompatibility = JavaVersion.VERSION_1_9
targetCompatibility = JavaVersion.VERSION_1_9

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:Hoxton.SR10"
    }
}

repositories {
    // if you have a dependency that's locally-built using maven, uncomment this to use local .m2 directory cache:
    // mavenLocal()
    mavenCentral()
    maven {
        url "https://repository.apache.org/content/repositories/releases"
    }
}

dependencies {
    // Google cloud libraries BOM:
    api platform("com.google.cloud:libraries-bom:16.4.0")
    // Dependencies that use this BOM:
    api "com.google.cloud:google-cloud-speech"
    api "com.google.cloud:google-cloud-storage"

    // Dependencies that are managed by io.spring.dependency-management (defined in plugins):
    implementation "ch.qos.logback:logback-access"
    implementation "com.fasterxml.jackson.core:jackson-databind"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jdk8"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin"
    implementation "org.springframework.boot:spring-boot-configuration-processor"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.retry:spring-retry"
    runtimeOnly "org.postgresql:postgresql"

    //Dependencies that are managed by io.springframework.cloud:spring-cloud-dependencies
    implementation "org.springframework.cloud:spring-cloud-starter-openfeign"
    implementation "org.springframework.cloud:spring-cloud-starter-kubernetes"
    implementation "org.springframework.cloud:spring-cloud-kubernetes-config"
    implementation "org.springframework.cloud:spring-cloud-kubernetes-ribbon"

    // Normal dependencies:
    implementation "com.amazonaws:aws-java-sdk-s3:1.11.959"
    //The following AWS dependencies are not directly used, but the aws-java-sdk pulls older versions, resulting in prod issues.
    implementation "com.amazonaws:aws-java-sdk-kms:1.11.959"
    implementation "com.amazonaws:aws-java-sdk-core:1.11.959"
    implementation "com.amazonaws:jmespath-java:1.11.959"
    implementation "com.h2database:h2:1.4.200"
    implementation "com.vladmihalcea:hibernate-types-52:2.10.2"
    implementation "javax.xml.bind:jaxb-api:2.3.1"
    implementation "io.sentry:sentry-spring:4.1.0"
    implementation "io.sentry:sentry-logback:4.1.0"
    implementation "io.springfox:springfox-swagger2:3.0.0"
    implementation "io.springfox:springfox-swagger-ui:3.0.0"
    implementation "net.bramp.ffmpeg:ffmpeg:0.6.2"
    implementation "net.logstash.logback:logstash-logback-encoder:6.6"
    implementation "org.axonframework:axon-core:3.3.7"
    implementation "org.axonframework:axon-spring-boot-starter:3.3.7"
    implementation "org.hibernate:hibernate-java8:5.4.28.Final"
    implementation "org.javamoney:moneta:1.4.2"
    implementation "org.javassist:javassist:3.27.0-GA"
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.4.30"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.4.30"
    compileOnly "org.projectlombok:lombok:1.18.18"
    annotationProcessor "org.projectlombok:lombok:1.18.18"
    implementation "org.springframework.cloud:spring-cloud-starter-bootstrap:3.0.1"
    implementation "org.zalando:jackson-datatype-money:1.2.1"

    // Test-only dependencies:
    testImplementation "io.mockk:mockk:1.10.6"
    testImplementation "org.assertj:assertj-core:3.19.0"
    testImplementation "org.axonframework:axon-test:3.3.7"
    testImplementation "org.jetbrains.kotlin:kotlin-test:1.4.30"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:1.4.30"
    testCompileOnly "org.projectlombok:lombok:1.18.18"
    testAnnotationProcessor "org.projectlombok:lombok:1.18.18"
    testImplementation("org.springframework.boot:spring-boot-starter-test:2.3.5.RELEASE") {
        exclude group: 'org.assertj', module: 'assertj-core'
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

// I don't think this is needed, but we can remove later. Rather "belt and braces" in prod at first.
bootJar {
    enabled = true
}

allOpen {
    annotation("org.springframework.context.annotation.Configuration")
    annotation("javax.persistence.Entity")
}
noArg {
    annotation("javax.persistence.Entity")
}
